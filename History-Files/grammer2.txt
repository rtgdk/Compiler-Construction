<mainFunction> -> MAIN SQO SQC <stmtAndFunctionDefs> END .
<stmtAndFunctionDefs> -> <stmtOrFunctionDef> <moreStmtOrFunctionDef> .
<moreStmtOrFunctionDef> -> <stmtOrFunctionDef> <moreStmtOrFunctionDef> | EPSILON .
<stmtOrFunctionDef> -> <stmt> | <functionDef> .
<stmt> -> <declarationStmt> | <assignmentStmt_type1> | <assignmentStmt_type2> | <ifStmt> | <ioStmt> | <funCallStmt> SEMICOLON .
<functionDef> -> FUNCTION SQO <parameter_list> SQC ASSIGNOP FUNID SQO <parameter_list> SQC <stmtAndFunctionDefs> END SEMICOLON .
<parameter_list> -> <type> ID <remainingList> .
<type> -> INT | REAL | STRING | MATRIX .
<remainingList> -> COMMA <type> ID <remainingList> | EPSILON .
<declarationStmt> -> <type> <var_list> SEMICOLON .
<var_list> -> ID <more_ids> .
<more_ids> -> COMMA ID <more_ids> | EPSILON .
<assignmentStmt_type1> -> <leftHandSide_singleVar> ASSIGNOP <rightHandSide_type1> SEMICOLON .
<assignmentStmt_type2> -> <leftHandSide_listVar> ASSIGNOP <rightHandSide_type2> SEMICOLON .
<leftHandSide_singleVar> -> ID .
<leftHandSide_listVar> -> SQO <var_list> SQC .
<rightHandSide_type1> -> <arithmeticExpression> | <sizeExpression> | <funCallStmt> .
<rightHandSide_type2> -> <sizeExpression> | <funCallStmt> .
<sizeExpression> -> SIZE ID .
<ifStmt> -> IF OP <booleanExpression> CL <stmt> <otherStmts> <elseStmt> .
<elseStmt> -> ELSE <stmt> <otherStmts> ENDIF SEMICOLON | ENDIF SEMICOLON .
<otherStmts> -> <stmt> <otherStmts> | EPSILON .
<ioStmt> -> READ OP ID CL SEMICOLON | PRINT OP ID CL SEMICOLON .
<funCallStmt> -> FUNID OP <inputParameterList> CL .
<inputParameterList> -> <var> <listVar> | EPSILON .
<listVar> -> COMMA <var> <listVar> | EPSILON .
<arithmeticExpression> -> <arithmeticTerm> <moreTerms_one> .
<moreTerms_one> -> <operator_lowPrecedence> <arithmeticTerm> <moreTerms_one> | EPSILON .
<arithmeticTerm> -> <arithmeticFactor> <moreTerms_two> .
<moreTerms_two> -> <operator_highPrecedence> <arithmeticFactor> <moreTerms_two> | EPSILON .
<arithmeticFactor> -> OP <arithmeticExpression> CL | <var> .
<operator_lowPrecedence> -> PLUS | MINUS .
<operator_highPrecedence> -> MUL | DIV .
<booleanExpression> -> OP <booleanExpression> CL <logicalOp> OP <booleanExpression> CL | NOT OP <booleanExpression> CL | <constrainedVars> <relationalOp> <constrainedVars> .
<constrainedVars> -> ID | NUM | RNUM .
<var> -> ID <matrixElement> | NUM | RNUM | STR | <matrix> .
<matrix> -> SQO <rows> SQC .
<rows> -> <row> <moreRows> .
<moreRows> -> SEMICOLON <row> <moreRows> | EPSILON .
<row> -> NUM <remainingColElements> .
<remainingColElements> -> COMMA NUM <remainingColElements> |  EPSILON .
<matrixElement> -> ID SQO NUM COMMA NUM SQC | EPSILON .
<logicalOp> -> AND | OR .
<relationalOp> -> LT | LE | EQ | GT | GE | NE .